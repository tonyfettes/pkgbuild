# Ruijie Yu (first.last@outlook.com)
pkgname=cajviewer-app-image
_pkgname=${pkgname%-app-image}
pkgver=20201022.0.0
pkgrel=1
pkgdesc='Document Viewer for TEB, CAJ, NH, KDH and PDF format'
url="https://cajviewer.cnki.net/download.html"
arch=(any)
license=('Proprietary')
depends=(
  'fuse2'
  'libglvnd'
  'libx11'
  'libxcb'
  'libxau'
  'libxdmcp'
)
makedepends=(
    'curl'
)
provides=("$_pkgname")
conflicts=("$_pkgname")
source=(
  "https://download.cnki.net/CAJViewer-x86_64-buildubuntu1604-201202.AppImage"
)
sha512sums=('SKIP')

# stripping leads to errors unfortunately
options=('!strip')

_install_path="/opt/$_pkgname"
prepare() {
  filename="$(basename "${source[0]}")"
  cd "$srcdir"
  chmod +x "$filename" && "./$filename" --appimage-extract
	find "squashfs-root/usr/share/icons/hicolor" -type d -exec chmod 755 {} \;
}

package() {
  squash_path="$srcdir/squashfs-root"
  dest_path="$pkgdir/$_install_path"
  
  # don't bother whether executable bits are necessary from upstream
  # install executable files as-is
  find "$squash_path/usr" -type f -executable \
      -exec realpath --relative-to "$squash_path" {} \; \
      | xargs -I{} install -Dm0755 "$squash_path/{}" "$dest_path/{}"
  # install nonexecutable files as-is
  find "$squash_path/usr" -type f -not -executable \
      -exec realpath --relative-to "$squash_path" {} \; \
      | xargs -I{} install -Dm0644 "$squash_path/{}" "$dest_path/{}"
  
  # install launch script
  bin_path="$pkgdir/usr/bin/"
  install -dm0755 "$bin_path"
  ln -st "$bin_path" "$_install_path/usr/bin/cajviewer"

  # Install icons and desktop
  install -dm755 "${pkgdir}/usr/share/icons"
  install -Dm644 "squashfs-root/${_pkgname}.desktop" "${pkgdir}/usr/share/applications/${_pkgname}.desktop"
	cp -R "squashfs-root/usr/share/icons/hicolor" "${pkgdir}/usr/share/icons"
}
