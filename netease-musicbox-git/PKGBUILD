# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Ariel AxionL <axionl@aosc.io>

pkgname=netease-musicbox-git
_gitname=musicbox
pkgver=r607.b890746
pkgrel=1
pkgdesc="A sexy command line interface musicbox for NetEase based on Python"
arch=(any)
url="https://github.com/tonyfettes/musicbox"
depends=('mpg123' 'python-pycryptodomex' 'python-requests' 'python-future' 'python-requests-cache' 'python-fuzzywuzzy' 'python-levenshtein')
makedepends=('python-poetry' 'git')
optdepends=('aria2: music caching'
            'libnotify: notifications'
            'qt5-base: lyrics support'
            'python-pyqt5: lyrics support'
            'python-dbus: lyrics support')
provides=('netease-musicbox')
conflicts=('netease-musicbox')
install=${pkgname}.install
license=('MIT')

#source=("${_gitname}::git+https://github.com/tonyfettes/musicbox#branch=wjy_test"
#source=("${_gitname}::git+https://github.com/tonyfettes/musicbox#branch=wjy_kwph"
source=("${_gitname}::git+https://github.com/tonyfettes/musicbox#branch=master"
        "LICENSE::https://raw.githubusercontent.com/tonyfettes/musicbox/master/LICENSE"
        "0001-Replace_pyqt4_to_pyqt5_as_depends.patch")

sha256sums=('SKIP'
            'e6ce649439eace57267c94e651e38370582f883e6341b3203e951b9497433641'
            '485d7a1e900da50a4d6d055131ebc3b2072cf5760d9e27cd266ff373aedb6d46')

pkgver() {
  cd "${srcdir}/${_gitname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "${srcdir}/${_gitname}"
  local _pkgver=$(sed -n 's/version = "\(.*\)"/\1/p' pyproject.toml)
  local _pkgname="NetEase-MusicBox"
  patch -Np1 -i "${srcdir}/0001-Replace_pyqt4_to_pyqt5_as_depends.patch"
  sed -i "s/version = get_current_version()/version = \"${_pkgver}\"/" ./NEMbox/__main__.py
}

build() {
  cd "${srcdir}/${_gitname}"
  local _pkgver=$(sed -n 's/version = "\(.*\)"/\1/p' pyproject.toml)
  local _pkgname="NetEase-MusicBox"
  poetry build -f sdist
  cd dist
  tar xf "${_pkgname}-${_pkgver}.tar.gz"
}

package() {
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  cd "${srcdir}/${_gitname}"
  local _pkgver=$(sed -n 's/version = "\(.*\)"/\1/p' pyproject.toml)
  local _pkgname="NetEase-MusicBox"
  cd "dist/${_pkgname}-${_pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1
}
# vim:set ts=2 sw=2 et:
